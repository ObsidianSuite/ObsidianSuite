buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
	
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'
	compileJava {
			sourceCompatibility = '1.8'
			targetCompatibility = '1.8'
	}
}

subprojects {
	apply plugin: 'forge'
}

dependencies {
   compile project(':API')
   compile project(':Animator')
   compile project(':Mod')
}

task eclipse(overwrite: true) {
	outputs.file "$projectDir/eclipse"

	def zipFile = file('tmp.zip')
	def url = new URL('https://github.com/ObsidianSuite/ObsidianSuite/releases/download/v1.0/ObsidianSuiteEclipseFolder.zip')
	
	doFirst {
		if (!zipFile.exists()) {
			url.withInputStream{ i -> zipFile.withOutputStream{ it << i }}
		}
		
		copy {
			from zipTree(zipFile) into projectDir
		}
		
		mkdir "Animator/run"
		mkdir "Mod/run"
	}
	
	doLast {
		delete zipFile
	}
}

def animatorProjects = [project(':API'),project(':Animator')]
animatorProjects.each { subproject -> evaluationDependsOn(subproject.path)}
task buildAnimatorWithAPI( type: Jar, dependsOn: animatorProjects.tasks["build"] ) {
    baseName = 'ObsidianAnimator_v0.3.4-Alpha'
    animatorProjects.each { subproject -> from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}
